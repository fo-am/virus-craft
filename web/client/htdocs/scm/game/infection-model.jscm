;; -*- mode: scheme; -*-
;; Viruscraft Copyright (C) 2017 FoAM Kernow

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; the main infection and population model

(define receptor-types (list 'circle 'triangle 'square 'donut 'guitar))
(define host-species-types (list 'sea 'land 'land2))
(define virus-max-receptors 10)

(define size-conv 0.003)

;; the host's spatial constants 
(define min-host-health 0)
(define max-host-health 50)
(define host-normal-growth 10) ;; units per second
(define host-age-decline -1)
(define host-health-start 10)
(define host-decline-age 60)
(define host-spawn-min-distance 0.05)
(define spawn-dist 0.2)
(define host-spawn-dist-tries 5)
(define host-move-distance 0.08)
(define host-movement-likelihood 0.4)

;; main disease parameters
(define infect-likelihood-mult 10) ;; per second mult
(define infect-spawn-reduction 0.75)
(define infection-distance 0.35)
(define host-infection-decline -1) ; (* deadliness)

(define host-loc-lat-safe 0.1)
(define host-loc-lat-width (- (* Math.PI 1) (* 2 host-loc-lat-safe)))
(define host-loc-lng-safe 0.1)
(define host-loc-lng-width (- (* Math.PI 2) (* 2 host-loc-lng-safe)))

(define new-organism-likelihood 3) ;; percent per second
(define mutation-rate 10)
(define susceptibility-slope 2)

;;(define susceptibility-prob-factor 0.2)
(define max-pop-size 100)


;; hacky global state 
(define cross-species-infection-happened #f)
(define transmission-ids (list 0 0))

;;;;;;;;;;;;;;;;;;;
;; minimal geometry

(define (sq x) (* x x))

(define (vec2-dist a b)
  (sqrt (+ (sq (- (vx a) (vx b)))
	   (sq (- (vy a) (vy b))))))

(define (vec2-squashed-dist a b)
  (sqrt (+ (sq (- (vx a) (vx b)))
	   (sq (/ (- (vy a) (vy b)) 2)))))

(define two-pi (* 2 3.141))

;;(define (random-point-nearby position)
;;  (vector
;;   (+ (vx position) 0.05)
;;   (+ (vy position) 0)
;;   0))

;;(vector
;; (+ (vx position) (* spawn-dist (sin (* (rndf) two-pi))))
;; (+ (vy position) (* spawn-dist (cos (* (rndf) two-pi)))) 
;; 0)

;;;;;;;;;;;;;;;;;;;
;; receptor types

(define (pick-random-receptor-type)
  (list-ref receptor-types (random (length receptor-types))))

;;;;;;;;;;;;;;;;;;;
;; virus
;; our virus simply consists of a set of keys for receptors we lock on to

(define (make-virus keys) (list keys))
(define (virus-keys v) (list-ref v 0))

(define (make-random-virus size)
  (make-virus (build-list size (lambda (_) (pick-random-receptor-type)))))

(define (make-virus-freq freq)
  (let ((c -1))
    (make-virus
     (foldl
      (lambda (f r)
	(set! c (+ c 1))
	(append (build-list f (lambda (_) (list-ref receptor-types c))) r))
      '() freq))))

;; this is simply based on the number of receptors to provide
;; a cost for generalist viruses
(define (virus-deadliness virus)
  1)
;;  (pow (/ (length (virus-keys virus)) virus-max-receptors) 2))
  
;;;;;;;;;;;;;;;;;;;
;; host
;; a host organism, which may be susceptible to a virus

(define current-host-id 0)
(define (make-host-id) 
  (set! current-host-id (+ current-host-id 1))
  current-host-id)

(define (make-host id receptor species infected pos health age moved last-pos)
  (list id receptor species infected pos health age moved last-pos))

(define (host-id h) (list-ref h 0))
(define (host-receptor h) (list-ref h 1))
(define (host-species h) (list-ref h 2))
(define (host-infected? h) (list-ref h 3))
(define (host-location h) (list-ref h 4))
(define (host-health h) (list-ref h 5))
(define (host-age h) (list-ref h 6))
(define (host-moved? h) (list-ref h 7))
(define (host-last-location h) (list-ref h 8))

;;;;;;;;;;;;;;;;;;;
;; host model

;; need tradeoff 

;; returns the susceptibility of the host of infection to the supplied virus
;; susceptibility will range from 0..1 with 0 being completely immune
;; pow give us a slope so that 1 is always 1 but we reduce the likelyhood
;; with fewer receptors - not science
(define (host-susceptibility host virus)
  (pow 
   (/ (foldl 
       (lambda (key r)
	 ;; loop over the keys, counting the ones that match
	 ;; we need to continously do this as the virus is being
	 ;; mutated by the player
	 (+ r (if (eq? key (host-receptor host)) 1 0)))
       0
       (virus-keys virus))
      virus-max-receptors)
   susceptibility-slope))

(define (host-susceptible? host virus)
  (> (host-susceptibility host virus) 0))
 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; reproduction and death of host organism

;; returns an updated health based on infection and age constraints
(define (host-calculate-health host susceptibility deadliness)
  ;;(msg (* host-infection-decline deadliness))
  (cond
   ;; old age
   ((> (host-age host) host-decline-age) 
    (max min-host-health (+ (host-health host) (* canvas-delta host-age-decline))))
   ((host-infected? host) 
    ;; uniform reduction in health no matter how susceptible
    ;; might be better to change this see todo
    (max min-host-health 
	 (+ (host-health host) 
	    (* canvas-delta host-infection-decline deadliness))))
   ((>= (host-health host) max-host-health) max-host-health)
   (else (+ (host-health host) (* canvas-delta host-normal-growth)))))

(define (host-alive? host)
  (> (host-health host) min-host-health))

;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; creating new organism

;;  (if (host-infected? host))
;;      #f ;; infected hosts can't reproduce

(define (host-can-breed? host)
  ;; if infected hosts can breed then super virulent viruses don't wipe out populations
  (if (host-infected? host)
      (< (random 100) (* canvas-delta new-organism-likelihood infect-spawn-reduction))
      (< (random 100) (* canvas-delta new-organism-likelihood))))

;; check if a point is within bounding box and 
;; far enough away from other hosts
;;(define (check-location location population threshold)
;;  (if (and (> (vx location) host-loc-lat-safe) 
;;	   (< (vx location) (+ host-loc-lat-safe host-loc-lat-width))
;;	   (> (vy location) host-loc-lng-safe) 
;;	   (< (vy location) (+ host-loc-lng-safe host-loc-lng-width)))
      ;; (foldl
      ;;  (lambda (host r)
      ;; 	 (if (and r (> (vec2-squashed-dist (host-location host) location) 
      ;; 		       threshold))
      ;; 	     #t #f))
      ;;  #t
      ;;  population)
      ;; #f))

;; find a space far enough away from other hosts, returns #f if 
;; there are none present

;; we don't care about collisions any more as everyone is moving around
(define (find-free-location position population distance num-tries)
  (vector
   (max host-loc-lat-safe 
	(modulo 
	 (+ (vx position) (* distance (sin (* (rndf) two-pi)))) 
	 host-loc-lat-width))
   (max host-loc-lng-safe
	(modulo 
	 (+ (vy position) (* distance (cos (* (rndf) two-pi)))) 
	 host-loc-lng-width))
   0))


;(let ((point (random-point-nearby position)))
;  (cond
;   ;; give up!!
;   ((< num-tries 1) #f)
;   ((check-location point population distance) point)
;   (else (find-free-location position population distance (- num-tries 1)))))

;; returns a new, mutated host organism
(define (host-new-organism host population location)
  (make-host
   (make-host-id)
   (if (< (random 100) mutation-rate)
       (pick-random-receptor-type)
       (host-receptor host))
   (host-species host) ;; maintain species type
   #f ;; infected should always be false as infected
      ;; species can't generate new species (at the moment)
   ;; pick a new position near to the parent species
   location
   host-health-start
   0
   #t
   location))

(define (host-update host virus population world-position-checker)
  (let ((susceptibility (host-susceptibility host virus))
	(new-location
	 ;; shall we move somewhere??
	 (if (< (rndf) (* canvas-delta host-movement-likelihood))
	     ;; use spawn distance for the moment, probably good enough distance
	     (let ((location (find-free-location 
			      (host-location host) 
			      population host-move-distance 1)))
	       ;; only move if we can
	       ;; now check that we can live there
	       (if (and location (world-position-checker (host-species host) location))
		   location
		   #f))
	     #f)))    
    ;;(msg susceptibility)
    (make-host
     (host-id host)
     (host-receptor host) ;; doesn't change
     (host-species host)
     (cond
      ((host-infected? host) #t)
      ;; randomise check based on suscept
      ((< (rndf) (* canvas-delta susceptibility infect-likelihood-mult))
       (population-is-infection-present? host population))
      (else #f))
     (if new-location new-location (host-location host))
     (host-calculate-health host susceptibility (virus-deadliness virus))
     (+ (host-age host) canvas-delta)
     (if new-location #t #f)
     (if new-location 
	 (host-location host) 
	 (host-last-location host)))))

;; for background infection, doesn't check sus
(define (host-infect host)
  (make-host
   (host-id host)
   (host-receptor host) 
   (host-species host) 
   #t
   (host-location host)
   (host-health host)
   (host-age host)
   (host-moved? host)
   (host-last-location host)))

(define (find-world-position species world-position-checker)
  ;; loop round until we find a position - todo: will hang!!
  (let ((pos (vector
	      (+ host-loc-lat-safe (* (rndf) host-loc-lat-width))
	      (+ host-loc-lng-safe (* (rndf) host-loc-lng-width)) 
	      0)))
    (if (world-position-checker species pos) 
	pos (find-world-position species world-position-checker))))

(define (make-random-host world-position-checker)
  (let ((species (choose host-species-types)))
    (let ((location (find-world-position species world-position-checker)))  
      (make-host 
       (make-host-id)
       (pick-random-receptor-type)
       species
       #f
       location
       (+ host-health-start (random (- max-host-health host-health-start)))
       (random host-decline-age)
       #t
       location))))

(define (make-grid-host i width)
  (make-host 
   (make-host-id)
   (pick-random-receptor-type)
   'sea
   ;;(choose host-species-types)
   #f
   (vector (* (quotient i 10) width) (* (modulo i 10) width) 0)
   host-health-start
   (random host-decline-age)
   #t
   (vector (* (quotient i 10) width) (* (modulo i 10) width) 0)))

(define (host-print host)
  (when (host-infected? host) (display "X")(display " "))
  (display (host-receptor host))(display " ")
  (display (host-health host))(display " ")(display (host-location host))
  (newline))

(define (host-search id l)
  (cond
   ((null? l) #f)
   ((eq? (host-id (car l)) id) (car l))
   (else (host-search id (cdr l)))))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;; main population

(define (make-random-population size world-position-checker)
  (build-list size (lambda (_) (make-random-host world-position-checker))))

(define (make-grid-population size width)
  (build-list size (lambda (i) (make-grid-host i width))))

(define (population-collect-new-organisms population world-position-checker)
  (foldl
   (lambda (host r)
     (if (host-can-breed? host)
	 ;; first find a location free of other organisms
	 (let ((location (find-free-location 
			  (host-location host) population 
			  host-spawn-min-distance host-spawn-dist-tries)))
	   ;; now check that we can live there
	   (if (and location (world-position-checker (host-species host) location))
	       (cons (host-new-organism host population location) r) r))
	 r))
   '()
   population))

(define (check-cross-species-infection!)
  (let ((r cross-species-infection-happened))
    (set! cross-species-infection-happened #f)
    r))

;; are any hosts in infection distance infected?
(define (population-is-infection-present? host population)
  (foldl
   (lambda (other-host infection)
     (if (and (not infection)
              (host-infected? other-host)
	      (< (vec2-squashed-dist (host-location host) 
			    (host-location other-host))
		 infection-distance))
	 (begin 
	   ;; set a bunch of globals to keep track of what's happening :[
	   (set! transmission-ids (list (host-id host) (host-id other-host)))
	   (when (not (eq? (host-species host)
			   (host-species other-host)))
		 (set! cross-species-infection-happened (list (host-id other-host) (host-id host))))
	   #t) 
	 infection))
   #f
   population))

(define (population-in-infection-range? host population)
  (foldl
   (lambda (other-host infection)
     (if (and (not infection)
              (host-infected? other-host)
	      (< (vec2-squashed-dist (host-location host) 
			    (host-location other-host))
		 infection-distance))
	   #t infection))
   #f
   population))

;; requires a function to check the new position for organisms
(define (population-update population virus world-position-checker)
  (append
   (if (< (length population) max-pop-size)
       (population-collect-new-organisms 
	population 
	world-position-checker) ;; add new organisms
       '())
   (map
    (lambda (host)
      (host-update host virus population world-position-checker)) ;; update organisms
    (filter
     host-alive? ;; remove dead host organisms
     ;; do this before main update so we can detect dead ones 
     ;; for the visualisation in order to remove them
     population))))

(define (population-print population)
  (for-each
   (lambda (host)
     (host-print host))
   population))

(define (population-infect population virus infection-rate)
  (map
   (lambda (host)
     (if (and (< (random 100) infection-rate)
	      (host-susceptible? host virus))
	 (host-infect host)
	 host))
   population))

;; force an infection at the start of the game...
(define (population-infect-one population virus)
  (define (_ pop)
    (cond
     ((null? pop) '())
     ((host-susceptible? (car pop) virus)
      (set! transmission-ids (list (host-id (car pop)) (host-id (car pop)))) 
      (cons (host-infect (car pop)) (cdr pop)))
     (else (cons (car pop) (_ (cdr pop))))))
  (_ population))

(define (population-num-infected population)
  (foldl
   (lambda (host r)
     (if (host-infected? host) (+ r 1) r))
   0
   population))

